package '3a-Function-based Behavior' {
	
	// ITEMS
	
	class FuelCmd;
	class EngineTorque;
	class TransmissionTorque;
	class DriveshaftTorque;
	class WheelTorque;
	
	// SIGNALS
	
	class EngineStart;
	class EngineOff;
	
	// BEHAVIORS
	
	behavior 'Generate Torque'(in fuelCmd: FuelCmd, out engineTorque: EngineTorque);
	behavior 'Amplify Torque'(in engineTorque: EngineTorque, out transmissionTorque: TransmissionTorque);
	behavior 'Transfer Torque'(in transmissionTorque: TransmissionTorque, out driveshaftTorque: DriveshaftTorque);
	behavior 'Distribute Torque'(in driveShaftTorque: DriveshaftTorque, out wheelTorque: WheelTorque[2]);
	
	behavior 'Provide Power'(in fuelCmd: FuelCmd, out wheelTorque1: WheelTorque, out wheelTorque2: WheelTorque) {
		step 'generate torque': 'Generate Torque'(in fuelCmd = 'Provide Power'::fuelCmd, out engineTorque);
		
		stream 'generate torque'::engineTorque to 'amplify torque'::engineTorque1;
				
		step 'amplify torque': 'Amplify Torque'(in engineTorque1, out transmissionTorque);
		
		stream 'amplify torque'::transmissionTorque to 'transfer torque'::transmissionTorque;
		
		step 'transfer torque': 'Transfer Torque'(in transmissionTorque, out driveshaftTorque);
		
		stream 'transfer torque'::driveshaftTorque to 'distribute torque'::driveshaftTorque;
		
		step 'distribute torque': 'Distribute Torque'(in driveshaftTorque, 
			out wheelTorque1 = 'Provide Power'::wheelTorque1, out wheelTorque2 = 'Provide Power'::wheelTorque2); 
	}
	
	behavior 'Provide Power With Control'(in fuelCmd: FuelCmd, out wheelTorque1: WheelTorque, out wheelTorque2: WheelTorque) {
		import Base::*;
		import Transfers::Transfer;
		import BaseFunctions::*;
		
		step 'provide power': 'Provide Power'[*] (
			in fuelCmd = 'Provide Power With Control'::fuelCmd, 
			out wheelTorque1 = 'Provide Power With Control'::wheelTorque1,
			out wheelTorque2 = 'Provide Power With Control'::wheelTorque2
		);
		
		feature self: 'Provide Power With Control' redefines Base::self;
		
		step engineStart: Transfer[*] ordered {
			end transferSource: Occurrence redefines Transfer::transferSource; 
			end transferTarget: Occurrence redefines Transfer::transferTarget = self;
		}
		
		step engineOff: Transfer[*] ordered {
			end transferSource: Occurrence redefines Transfer::transferSource;
			end transferTarget: Occurrence redefines Transfer::transferTarget = self;
		}
		
		step firstEngineStart[1] subsets engineStart = first(engineStart);
		step restEngineStart[*] subsets engineStart = rest(engineStart);
		
		succession engineStart[1] to engineOff[1];
		succession engineOff[1] to restEngineStart[1];		
		
		succession engineStart[1] to 'provide power'[1];
		succession 'provide power'[1] to engineOff[1];
	}
}