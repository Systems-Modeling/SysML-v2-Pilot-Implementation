// Lark grammar for KerML and SysML EBNF (textual notation)
//
// Copyright (c) 2025: DEKonsult
//
// Author: Hans Peter de Koning (DEKonsult)
//

start: _rule+

_rule: lexical_rule _NON_CONTINUATION_LINE | nonterminal_rule _NON_CONTINUATION_LINE | line_comment

lexical_rule: LEXICAL_NAME "=" alternation

nonterminal_rule: NONTERMINAL_NAME ( ":" abstract_element )? "=" alternation

line_comment: LINE_COMMENT _NL+

abstract_element: NONTERMINAL_NAME

?alternation: sequence ( "|" sequence )*

?sequence: expression+

?expression: atom repetition?
           | "(" alternation ")" repetition?
           | prop_assign
           | prop_list_append
           | boolean_prop_assign
           | nonparsing_prop_assign
           | nonparsing_prop_list_append
           | nonparsing_empty_value

?atom: name
     | qualified_name
     | terminal

repetition: REPETITION_SYMBOL

name: LEXICAL_NAME | NONTERMINAL_NAME

qualified_name: QUALIFIED_NAME | "~" QUALIFIED_NAME

terminal: TERMINAL

prop_name: PROPERTY_NAME ( "." PROPERTY_NAME )*

prop_assign: prop_name "=" prop_value
prop_list_append: prop_name "+=" prop_value
boolean_prop_assign: prop_name "?=" prop_value
nonparsing_prop_assign: "{" prop_name "=" nonparsing_prop_value "}"
nonparsing_prop_list_append: "{" prop_name "+=" nonparsing_prop_value "}"
nonparsing_empty_value: "{" "}"

?prop_value: atom | BOOLEAN_VALUE | "(" atom ( "|" atom )+ ")"
?nonparsing_prop_value: terminal | prop_name | BOOLEAN_VALUE | THIS

// TERMINAL is any string containing one or more printable ASCII characters, except single quote, between two single quotes.
// The ASCII character range is defined as regex [ -&(-~] i.e. union of space-to-ampersand and left-parenthesis-to-tilda.
// A single quote itself must be escaped with a backslash.
TERMINAL: "'" /(\\'|[ -&(-~])+/ "'"
// NONTERMINAL_NAME is a PascalCase identifier containing alphas
NONTERMINAL_NAME: /[A-Z]([a-z]+[A-Z]?)+/
// LEXICAL_NAME is UPPER_SNAKE_CASE identifier containing alphas
LEXICAL_NAME: /[A-Z](_?[A-Z]+)+/
QUALIFIED_NAME: "[QualifiedName]"
// PROPERTY_NAME is a camelCase identifier containing alphas
PROPERTY_NAME: /[a-z][A-Za-z]*/
REPETITION_SYMBOL: "?" | "+" | "*"
BOOLEAN_VALUE: "false" | "true"
THIS: "this"

_CR: "\r"
_LF: "\n"
_NL: _CR? _LF
WS_INLINE: " " | "\t"
LINE_COMMENT: "//" /[^\r\n]*/

// A _NON_CONTINUATION_LINE is a line that does not start with a space or tab
// Use negative lookahead assertion re pattern (?!...) to not consume the possible match
_NON_CONTINUATION_LINE: ( _NL /(?![ \t])/ )+

// Ignore inline whitespace (space and tab) as well as continuation lines where next line starts with leading whitespace
%ignore WS_INLINE
%ignore _NL WS_INLINE+
