/*
 * generated by Xtext 2.12.0
 */
package org.omg.kerml.xtext.ui.outline

import org.eclipse.xtext.ui.editor.outline.impl.DefaultOutlineTreeProvider
import org.eclipse.xtext.ui.editor.outline.IOutlineNode
import org.omg.sysml.lang.sysml.Element
import org.omg.sysml.lang.sysml.Feature
import org.omg.sysml.lang.sysml.Generalization
import org.omg.sysml.lang.sysml.Membership
import org.omg.sysml.lang.sysml.Redefinition
import org.omg.sysml.lang.sysml.Import
import org.omg.sysml.lang.sysml.Subsetting
import org.omg.sysml.lang.sysml.OperatorExpression
import org.omg.sysml.lang.sysml.Relationship
import org.omg.sysml.lang.sysml.LiteralString
import org.omg.sysml.lang.sysml.LiteralBoolean
import org.omg.sysml.lang.sysml.LiteralInteger
import org.omg.sysml.lang.sysml.LiteralReal
import org.omg.sysml.lang.sysml.LiteralUnbounded
import org.omg.sysml.lang.sysml.FeatureMembership
import org.omg.sysml.lang.sysml.Expression
import org.omg.sysml.lang.sysml.Type
import org.omg.sysml.lang.sysml.VisibilityKind
import org.omg.sysml.lang.sysml.Classifier
import org.omg.sysml.lang.sysml.Annotation
import org.omg.sysml.lang.sysml.Conjugation
import org.omg.sysml.lang.sysml.Function
import org.omg.sysml.lang.sysml.NullExpression

/**
 * Customization of the default outline structure.
 *
 * See https://www.eclipse.org/Xtext/documentation/310_eclipse_support.html#outline
 */
class KerMLOutlineTreeProvider extends DefaultOutlineTreeProvider {

	def String _text(Element element) {
		var text = element.eClass.name;
		if (element.name !== null) {
			text += ' ' + element.name;
		}
		text 
	}
	
	def String _text(VisibilityKind visibility) {
		if (visibility == VisibilityKind.PACKAGE) "packaged"
		else visibility.toString
	}
	
	def String _text(Membership membership) {
		membership.prefixText + ' ' + membership.nameText
	}
	
	def String nameText(Membership membership) {
		if (membership.memberName !== null)
			membership.memberName
		else if (membership.memberElement?.name !== null)
			membership.memberElement.name
		else ""
	}
	
	def String prefixText(Membership membership) {
		var text = membership.eClass.name;
		if (membership.ownedMemberElement !== null) {
			text += ' owns'
		}
		if (membership.visibility !== null) {
			text += ' ' + membership.visibility._text;
		}
		text
	}
	
	def String prefixText(FeatureMembership membership) {
		var text = (membership as Membership).prefixText
		if (membership.isComposite) {
			text += ' composite'
		}
		if (membership.isPortion) {
			text += ' portion'
		}
		if (membership.isPort) {
			text += ' port'
		}
		if (membership.direction !== null) {
			text += ' ' + membership.direction
		}
		text
	}
	
	def String _text(Import import_) {
		var text = import_.eClass.name;
		if (import_.visibility !== null) {
			text += ' ' + import_.visibility._text
		}
		if (import_.importedPackage?.name !== null) {
			text += ' ' + import_.importedPackage.name
		}
		text
	}
	
	def String _text(Type type) {
		var text = type.eClass.name;
		if (type.isAbstract) {
			text += ' abstract'
		}
		if (type.name !== null) {
			text += ' ' + type.name;
		}
		text
	}
	
	def String _text(LiteralString literal) {
		"LiteralString " + literal.value
	}
	
	def String _text(LiteralBoolean literal) {
		"LiteralBoolean " + literal.value
	}
	
	def String _text(LiteralInteger literal) {
		"LiteralInteger " + literal.value
	}
	
	def String _text(LiteralReal literal) {
		"LiteralReal " + literal.value
	}
	
	def String _text(LiteralUnbounded literal) {
		"LiteralUnbounded *"
	}
	
	def String _text(NullExpression expression) {
		"NullExpression null"
	}
	
	def boolean _isLeaf(Annotation annotation) {
		false
	}
	
	def void _createChildren(IOutlineNode parentNode, Annotation annotation) {
		super._createChildren(parentNode, annotation)
		if (annotation.annotatedElement !== null) {
			createEObjectNode(parentNode, annotation.annotatedElement, 
				_image(annotation.annotatedElement), annotation.annotatedElement._text, 
				true
			)
		}
	}

	def boolean _isLeaf(Membership membership) {
		false
	}
	
	def void _createChildren(IOutlineNode parentNode, Membership membership) {
		super._createChildren(parentNode, membership)
		var memberElement = membership.memberElement;
		if (membership.ownedMemberElement === null && 
				memberElement !== null) {
			createEObjectNode(parentNode, memberElement, 
				memberElement._image, memberElement._text, 
				true
			)
		}
	}
	
	def boolean _isLeaf(Import _import) {
		_import.importedPackage === null && _import.ownedElement.isEmpty
	}
	
	def void _createChildren(IOutlineNode parentNode, Import _import) {
		super._createChildren(parentNode, _import)
		var importedPackage = _import.importedPackage;
		if (importedPackage !== null) {
			createEObjectNode(parentNode, importedPackage, 
				importedPackage._image, importedPackage._text, 
				_import.importOwningPackage == importedPackage || importedPackage._isLeaf
			)
		}
	}
	
	def void _createChildren(IOutlineNode parentNode, org.omg.sysml.lang.sysml.Package _package) {
		for (childElement : _package.eContents()) {
			if (!(childElement instanceof Import || childElement instanceof Membership)) {
				createNode(parentNode, childElement)
			}
		}
		for (_import: _package.ownedImport) {
			createEObjectNode(parentNode, _import, 
				_import._image, _import._text, 
				_import._isLeaf
			)
		}
		for (membership: _package.ownedMembership) {
			createEObjectNode(parentNode, membership, 
				membership._image, membership._text, 
				membership._isLeaf
			)
		}
	}
	
	def boolean _isLeaf(Classifier classifier) {
		// Ensure default subclassing
		classifier.ownedSuperclassing
		super._isLeaf(classifier)
	}
	
	def boolean _isLeaf(Function function) {
		// Ensure result connector
		function.feature
		_isLeaf(function as Classifier)
	}
	
	def boolean _isLeaf(Feature feature) {
		// Ensure default redefinition/subsetting
		feature.ownedSubsetting
		// Ensure valuation connector
		feature.feature
		super._isLeaf(feature)
	}
	
	def boolean _isLeaf(Generalization generalization) {
		generalization.general === null
	}
	
	def void _createChildren(IOutlineNode parentNode, Generalization generalization) {
		if (generalization.general !== null) {
			createEObjectNode(parentNode, generalization.general, 
				generalization.general._image, generalization.general._text, 
				true
			)
		}
	}
	
	def boolean _isLeaf(Redefinition redefinition) {
		redefinition.redefinedFeature === null
	}

	def void _createChildren(IOutlineNode parentNode, Redefinition redefinition) {
		if (redefinition.redefinedFeature !== null) {
			createEObjectNode(parentNode, redefinition.redefinedFeature, 
				redefinition.redefinedFeature._image, redefinition.redefinedFeature._text, 
				true
			)
		}
	}

	def boolean _isLeaf(Subsetting subset) {
		subset.subsettedFeature === null
	}

	def void _createChildren(IOutlineNode parentNode, Subsetting subset) {
		if (subset.subsettedFeature !== null) {
			createEObjectNode(parentNode, subset.subsettedFeature, 
				_image(subset.subsettedFeature), subset.subsettedFeature._text, 
				true
			)
		}
	}
	
	def boolean _isLeaf(Conjugation conjugation) {
		conjugation.originalType === null
	}
	
	def void _createChildren(IOutlineNode parentNode, Conjugation conjugation) {
		if (conjugation.originalType !== null) {
			createEObjectNode(parentNode, conjugation.originalType, 
				_image(conjugation.originalType), conjugation.originalType._text, 
				true
			)
		}
	}
	
	def boolean _isLeaf(Expression expression) {
		// Ensure derivation of inputs and outputs
		expression.input
		expression.output
		return _isLeaf(expression as Feature)
	}
	
	def boolean _isLeaf(OperatorExpression expression) {
		// Ensure derivation of typing
		expression.typing
		_isLeaf(expression as Expression) && expression.ownedMembership.isEmpty
	}
	
	def void _createChildren(IOutlineNode parentNode, OperatorExpression expression) {
		for (Relationship relationship : expression.ownedRelationship) {
			createEObjectNode(parentNode, relationship, 
				_image(relationship), 
				if (relationship instanceof Membership) (relationship as Membership)._text 
				else relationship._text, 
				false
			);
		}
	}

}
