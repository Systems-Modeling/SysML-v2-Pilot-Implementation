// Lark grammar for SysML Graphical BNF
//
// Copyright (c) 2025: DEKonsult
//
// Author: Hans Peter de Koning (DEKonsult)
//

start: (_rule? _NL)* _rule?

_rule: lexical | nonterminal | note

lexical: LEXICAL_NAME "=" alternation

nonterminal: GRAPHICAL_NAME ( "=" | "=|" ) alternation

note: NOTE

abstract_element: ":" NONTERMINAL_NAME

?alternation: sequence ( "|" sequence )*

?sequence: expression+

?expression: atom repetition?
           | "(" alternation ")" repetition?
           | prop_assign
           | prop_list_append
           | boolean_prop_assign
           | nonparsing_prop_assign
           | nonparsing_prop_list_append
           | nonparsing_empty_value
           | image

?atom: name
     | qualified_name
     | terminal

repetition: REPETITION_SYMBOL

name: LEXICAL_NAME | NONTERMINAL_NAME

qualified_name: QUALIFIED_NAME

terminal: TERMINAL

prop_assign: PROPERTY_NAME "=" prop_value
prop_list_append: PROPERTY_NAME "+=" prop_value
boolean_prop_assign: PROPERTY_NAME "?=" prop_value
nonparsing_prop_assign: "{" PROPERTY_NAME "=" nonparsing_prop_value "}"
nonparsing_prop_list_append: "{" PROPERTY_NAME "+=" nonparsing_prop_value "}"
nonparsing_empty_value: "{" "}"

?prop_value: atom | BOOLEAN_VALUE
?nonparsing_prop_value: terminal | PROPERTY_NAME | BOOLEAN_VALUE | THIS

image: IMAGE

TERMINAL: /'[^']+'/
GRAPHICAL_NAME: /[a-z][a-z\-]+/
NONTERMINAL_NAME: /[A-Za-z][A-Za-z\-]+/
LEXICAL_NAME: /[A-Z][A-Z0-9_]+/
QUALIFIED_NAME: "[QualifiedName]"
PROPERTY_NAME: /[a-z][A-Za-z0-9.]+/
REPETITION_SYMBOL: "?" | "+" | "*"
BOOLEAN_VALUE: "false" | "true"
THIS: "this"
NOTE: /\/\/[^\n]*/
_NL: /(\r?\n)+\s*/
IMAGE: /\<img.+\/\>/

// %import common.ESCAPED_STRING -> _STRING
// %import common.SIGNED_INT -> NUMBER
// %import common.WS_INLINE
%import common.WS

//COMMENT: /\s*/ "//" /[^\n]*/

%ignore WS
//%ignore WS_INLINE
//%ignore COMMENT
